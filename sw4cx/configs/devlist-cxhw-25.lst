# cxhw:25 -- klystrons

include(`g0401.devtype')
include(`g0603.devtype')
include(`v06xx.devtype')
include(`c0642.devtype')
include(`r0601.devtype')
include(`adc200.devtype')
include(`f4226.devtype')
include(`v5k5045.devtype')
include(`v5phaseconv.devtype')

dev phasedac c0642/remdrv ~ 6 b:v5-phr
dev phasectl v5phaseconv  ~ - phasedac
#cpoint ic.linRF.Phase1 phasectl.phase1
#cpoint ic.linRF.Phase2 phasectl.phase2
#cpoint ic.linRF.Phase3 phasectl.phase3
#cpoint ic.linRF.Phase4 phasectl.phase4


# 1:N 2:f4226_count 3:nominal 4/opt:gvi_params
define(`ONE_KLS', `
    # a. Devices
    dev k$1_pks g0603/remdrv  ~ 2  b:v5-l-kls$1 spd0=200 unipolar
    dev k$1_gvi g0401/remdrv  ~ 4  b:v5-l-kls$1 $4
    dev k$1_urr v06xx/remdrv  ~ 6  b:v5-l-kls$1
    dev k$1_dac c0642/remdrv  ~ 8  b:v5-l-kls$1
    dev k$1_in1 r0601/remdrv  ~ 10 b:v5-l-kls$1
    dev k$1_in2 r0601/remdrv  ~ 11 b:v5-l-kls$1
    dev k$1_osc adc200/remdrv ~ 13 b:v5-l-kls$1
ifelse(eval($2 >= 1), `0', `', `
    dev k$1_f_1 f4226/remdrv  ~ 15 b:v5-l-kls$1
')
ifelse(eval($2 >= 2), `0', `', `
    dev k$1_f_2 f4226/remdrv  ~ 16 b:v5-l-kls$1
')

    # b. A common hierarchy for v5k5045-required devices
    cpoint kls$1_devs {
        pks k$1_pks
        gvi k$1_gvi
        urr k$1_urr
        dac k$1_dac
        in1 k$1_in1
        in2 k$1_in2
    }

    # c. The "klystron device" itself
    dev kls$1 v5k5045 ~ - kls$1_devs nominal=$3

    cpoint kls$1.phase phasectl.phase$1

    # d. EmanoFedya-required cpoints
    cpoint ic.linRF.kls$1 {
        HV.set     kls$1.hvset
        power.set  kls$1.pwrset
        HV.RBV     kls$1.hvset_cur
        c1.dly.set kls$1.delay1
        c2.dly.set kls$1.delay2
        c3.dly.set kls$1.delay3
        phase      kls$1.phase
    }
')

ONE_KLS(1, 0, 45500, t_quant=100)
ONE_KLS(2, 1, 43500, t_quant=100)
ONE_KLS(3, 2, 44500, t_quant=100)
ONE_KLS(4, 0, 45900, t_quant=100)
